################################################################################
# Queries for GRAPH: http://purl.org/net/nlprepository/spl-core
#
# Core graph contains all SPLs (structured product labelings) label sections with metadata. They are directly comes from Dailymed product label in XMLs.
################################################################################

# Queries for product label content and metadata present in the LinkedSPLs RDF graph

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>

## Get metadata for the SPLs of all products containing a drug ##
SELECT ?label ?splId ?version ?setId ?org ?date ?homepage
FROM <http://purl.org/net/nlprepository/spl-core>
WHERE {

?splId rdfs:label ?label.
?splId dc:subject <%s>.  ## RxNORM URI, example: "http://purl.bioontology.org/ontology/RXNORM/197517"
?splId linkedspls_vocabulary:versionNumber ?version.
?splId linkedspls_vocabulary:setId ?setId.
?splId linkedspls_vocabulary:representedOrganization ?org.
?splId linkedspls_vocabulary:effectiveTime ?date.
?splId foaf:homepage ?homepage.

}

Results set in shorten URL: Results set in shorten URL: http://goo.gl/nU95fy

--------------------------------------------------------------------------------
Get the three sections of interest for a specific SPL ##
(substituting an ?splid value from the above query for %s. ##
For example, try <http://bio2rdf.org/linkedspls:2150f73a-179b-4afc-b8ce-67c85cc72f04>) ##
--------------------------------------------------------------------------------

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX loinc: <http://www.hipaaspace.com/Medical_Billing/Coding/Logical.Observation.Identifiers.Names.and.Codes/>

SELECT ?textClinicalStudies ?textDrugInteractions ?textClinicalPharmacology
FROM <http://purl.org/net/nlprepository/spl-core>
WHERE {

OPTIONAL { <http://bio2rdf.org/linkedspls:2150f73a-179b-4afc-b8ce-67c85cc72f04> loinc:34092-7 ?textClinicalStudies }
OPTIONAL { <http://bio2rdf.org/linkedspls:2150f73a-179b-4afc-b8ce-67c85cc72f04> loinc:34073-7 ?textDrugInteractions }
OPTIONAL { <http://bio2rdf.org/linkedspls:2150f73a-179b-4afc-b8ce-67c85cc72f04> loinc:34090-1 ?textClinicalPharmacology }

}
LIMIT 10


Results set in shorten URL: Results set in shorten URL: http://goo.gl/KfSGSR

--------------------------------------------------------------------------------
return all sections from all drug products containing clopidogrel 75 MG oral tablet that the FDA notes as having pharmgx information:
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>

SELECT DISTINCT ?setId ?biomarker ?sectionTag ?sectionLoinc ?sectionText

WHERE {
?s dc:subject <http://purl.bioontology.org/ontology/RXNORM/197446>.
?s linkedspls_vocabulary:setId ?setId.

?pharmgxData linkedspls_vocabulary:setId ?setId.
?pharmgxData linkedspls_vocabulary:pharmgxBiomarker ?biomarker.
?pharmgxData linkedspls_vocabulary:pharmgxSPLSection ?sectionLoinc.

?sectionLoinc rdfs:label ?sectionTag.
?s ?sectionLoinc ?sectionText.
}

Results set in shorten URL: http://goo.gl/9RFeL9

--------------------------------------------------------------------------------
# return all setIds and drug product rxcuis for product labels that contain sections with FDA pharmgx data for biomarker "CYPD6":
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>

SELECT DISTINCT ?setId ?rxcuiDrugProduct ?sectionTag ?sectionText 
WHERE {
?pharmgxData linkedspls_vocabulary:pharmgxBiomarker "CYP2D6".
?pharmgxData linkedspls_vocabulary:setId ?setId.
?pharmgxData linkedspls_vocabulary:pharmgxSPLSection ?sectionLoinc.
?sectionLoinc rdfs:label ?sectionTag.
?s ?sectionLoinc ?sectionText.

?spl linkedspls_vocabulary:setId ?setId.

?spl dc:subject ?rxcuiDrugProduct.

} LIMIT 10

Results set in shorten URL: http://goo.gl/Igwoh4

--------------------------------------------------------------------------------
get all sections w/ FDA pharmgx data for drugs with an antidepressant application.
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX ncbit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX fn:<http://www.w3.org/2005/xpath-functions#>

SELECT DISTINCT * 
WHERE {
?s linkedspls_vocabulary:setId ?setId.
?pharmgxData linkedspls_vocabulary:setId ?setId.
?pharmgxData linkedspls_vocabulary:therapeuticApplication ncbit:Psychiatry.
?pharmgxData linkedspls_vocabulary:activeMoietyRxCUI ?moiety.
?pharmgxData linkedspls_vocabulary:pharmgxBiomarker ?biomarker.
?pharmgxData linkedspls_vocabulary:pharmgxSPLSection ?sectionLoinc.
?sectionLoinc rdfs:label ?sectionTag.
?s ?sectionLoinc ?sectionText.

}
LIMIT 10

Results set in shorten URL: http://goo.gl/OzKUmR

--------------------------------------------------------------------------------
Get the PRO, HUGO, and Pharmgkb mappings for pharmgx biomarkers associated with TRIMIPRAMINE in product labeling
--------------------------------------------------------------------------------

PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT * WHERE {
?pharmgxData a linkedspls_vocabulary:pharmgxData;
linkedspls_vocabulary:pharmgxDrug "TRIMIPRAMINE";
linkedspls_vocabulary:pharmgxBiomarker ?biomarker;
linkedspls_vocabulary:pharmgxSPLSection ?splSect;
linkedspls_vocabulary:HGNCGeneSymbol ?genSymb;
owl:sameAs ?biomarkerMapping;
foaf:homepage ?home;
linkedspls_vocabulary:setId ?setId.
}

Results set in shorten URL: http://goo.gl/Uur5fX

################################################################################
# Queries for GRAPH: http://purl.org/net/nlprepository/spl-active-moiety
# active moiety graph is a RDF set that included mappings, (UNII, RxNorm, ChEBI, Drugbank, DrOn, OMOP and NDFRT), for active ingredients.
################################################################################


--------------------------------------------------------------------------------
what pharmgx biomarkers are common to PE classes?
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX ncbit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX ndfrt: <http://purl.bioontology.org/ontology/NDFRT/>

SELECT DISTINCT ?biomarker ?peURI
WHERE {
?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?s linkedspls_vocabulary:setId ?setId.

?activeMoiety <http://purl.bioontology.org/ontology/NDFRT/N0000009802> ?peURI.
?s linkedspls_vocabulary:fullName ?med.

?pharmgx linkedspls_vocabulary:setId ?setId.
?pharmgx linkedspls_vocabulary:pharmgxBiomarker ?biomarker.

}
LIMIT 100

Results set in shorten URL: http://goo.gl/krLPjh

--------------------------------------------------------------------------------
what pharmgx biomarkers are common to EPC classes?
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX ncbit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX ndfrt: <http://purl.bioontology.org/ontology/NDFRT/>

SELECT DISTINCT ?biomarker ?epcURI
WHERE {
?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?s linkedspls_vocabulary:setId ?setId.

?activeMoiety <http://purl.bioontology.org/ontology/NDFRT/N0000182631> ?epcURI.
?s linkedspls_vocabulary:fullName ?med.

?pharmgx linkedspls_vocabulary:setId ?setId.
?pharmgx linkedspls_vocabulary:pharmgxBiomarker ?biomarker.

}
LIMIT 100

Results set in shorten URL: http://goo.gl/54gUBL

--------------------------------------------------------------------------------
what pharmgx biomarkers are common to MoA (mechanism of action) classes?
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX ncbit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX ndfrt: <http://purl.bioontology.org/ontology/NDFRT/>

SELECT DISTINCT ?biomarker ?moaURI
WHERE {
?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?s linkedspls_vocabulary:setId ?setId.

?activeMoiety <http://purl.bioontology.org/ontology/NDFRT/N0000000223> ?moaURI.
?s linkedspls_vocabulary:fullName ?med.

?pharmgx linkedspls_vocabulary:setId ?setId.
?pharmgx linkedspls_vocabulary:pharmgxBiomarker ?biomarker.

}
LIMIT 100

Results set in shorten URL: http://goo.gl/N9vEiX

--------------------------------------------------------------------------------
what pharmgx biomarkers are common to Chemical/Ingredient classes (suggests how incomplete the class mapping is)?
--------------------------------------------------------------------------------

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX ncbit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX ndfrt: <http://purl.bioontology.org/ontology/NDFRT/>

SELECT DISTINCT ?biomarker ?ciURI
WHERE {
?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?s linkedspls_vocabulary:setId ?setId.

?activeMoiety <http://purl.bioontology.org/ontology/NDFRT/N0000000002> ?ciURI.
?s linkedspls_vocabulary:fullName ?med.

?pharmgx linkedspls_vocabulary:setId ?setId.
?pharmgx linkedspls_vocabulary:pharmgxBiomarker ?biomarker.

}
LIMIT 100

Results set in shorten URL: http://goo.gl/9ABxC9

--------------------------------------------------------------------------------
Get the most recent product label for DULOXETINE
--------------------------------------------------------------------------------

PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>

SELECT DISTINCT ?s ?date WHERE {

?s linkedspls_vocabulary:effectiveTime ?date.
?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?activeMoiety rdfs:label "DULOXETINE".

}
ORDER BY DESC(?date)
LIMIT 1

Results set in shorten URL: http://goo.gl/ZDoj0I

--------------------------------------------------------------------------------
chain that gets all sections containing pharmgx data from the most recent PI for warfarin and clopidogrel:
--------------------------------------------------------------------------------

PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?s ?setid ?date WHERE {
?s rdf:type linkedspls_vocabulary:structuredProductLabelMetadata;
linkedspls_vocabulary:setId ?setid;
linkedspls_vocabulary:effectiveTime ?date.

?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?activeMoiety rdfs:label "WARFARIN".

}
ORDER BY DESC(?date)
LIMIT 1

Results set in shorten URL: http://goo.gl/aWhdSR

--------------------------------------------------------------------------------
Join graph active moiety with pharmgxTable in core graph by dailymed setid
Query chain that gets all sections containing pharmgx data from the most recent PI with its setid
--------------------------------------------------------------------------------

PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>

SELECT ?rxcui ?sectionTag ?sectionLoinc ?sectionText WHERE {

?pharmgxData a linkedspls_vocabulary:pharmgxData;
linkedspls_vocabulary:setId "d655ff56-c17d-4331-8e9c-93e36f4ffeae";
linkedspls_vocabulary:pharmgxSPLSection ?sectionLoinc.

?sectionLoinc rdfs:label ?sectionTag.
?s ?sectionLoinc ?sectionText.

?s a linkedspls_vocabulary:structuredProductLabelMetadata;
linkedspls_vocabulary:setId "d655ff56-c17d-4331-8e9c-93e36f4ffeae".

?s linkedspls_vocabulary:activeMoiety ?activeMoiety.
?activeMoiety linkedspls_vocabulary:RxCUI ?rxcui.

}

Results set in shorten URL: http://goo.gl/JvhA9D

################################################################################
# GRAPH: http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014
# Pharmgx graph is a dataset in RDF that have pharmacists's annotations for dailymed product label
################################################################################

# PURPOSE: To demonstrate an interoperable annotation data set
# consisting of portions of product labels annotated by a team of
# pharmacists for pharmacogenomics statements. The annotations are
# published according to the W3C Open Data Annotation standard
# <http://www.openannotation.org/spec/core/>

--------------------------------------------------------------------------------
QUERY: Following the Open Annotation Data Model (www.openannotation.org/spec/beta/), each annotation links an annotation "body" to a "target." Show a complete annotation by enumerating the body and target URIs 
--------------------------------------------------------------------------------

PREFIX fn:<http://www.w3.org/2005/xpath-functions#>
PREFIX oa:<http://www.w3.org/ns/oa#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dikbD2R: <http://dbmi-icode-01.dbmi.pitt.edu:2020/vocab/resource/>
PREFIX domeo: <http://www.w3.org/ns/domeo#>
PREFIX pav: <http://purl.org/pav/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX ncit:<http://purl.bioontology.org/ontology/NCIT#>

SELECT ?item ?source ?text ?drug ?section ?gene ?biomarker ?pkImpact ?pdImpact ?drugRec ?doseRec ?testRec ?monitRec 
?variant ?prodrug ?medicationConcern ?variantFreq ?clinical ?medicalCondition ?testResult ?alleles ?comment
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>

WHERE {

?item rdf:type oa:SPLAnnotation;
       oa:hasTarget ?target;
       oa:hasBody ?body.

  ?target oa:hasSelector ?selector.

  ?selector oa:hasSource ?source;
       oa:exact ?text.

  ?body dailymed:pharmgxDrug ?drug;
        sio:SIO_000111 ?section.

      OPTIONAL {?body dailymed:HGNCGeneSymbol ?gene. }
      OPTIONAL {?body dailymed:pharmgxBiomarker ?biomarker. }
      OPTIONAL {?body poc:PharmacokineticImpact ?pkImpact. }
      OPTIONAL {?body poc:PharmacodynamicImpact ?pdImpact. }
      OPTIONAL {?body poc:MonitoringRecommendation ?monitRec. }
      OPTIONAL {?body poc:TestRecommendation ?testRec. }
      OPTIONAL {?body poc:DrugSelectionRecommendation ?drugRec. }
      OPTIONAL {?body poc:DoseSelectionRecommendation ?doseRec. }
      OPTIONAL {?body dailymed:ConcomitantMedicationOfConcern ?medicationConcern. }
      OPTIONAL {?body poc:VariantFrequency ?variantFreq. }
      OPTIONAL {?body dailymed:ClinicalTrial ?clinical. }
      OPTIONAL {?body poc:MedicalCondition ?medicalCondition. }
      OPTIONAL {?body poc:Variant ?variant. }
      OPTIONAL {?body poc:TestResult ?testResult. }
      OPTIONAL {?body dailymed:Prodrug ?prodrug. }
      OPTIONAL {?body poc:Comment ?comment. }
      OPTIONAL {?body ncit:Alleles ?alleles. }
}	
GROUP BY ?text
ORDER BY DESC(?item)

Results set in shorten URL: http://goo.gl/UTavRR

--------------------------------------------------------------------------------
QUERY: Get back the SPL URL for an annotation over a given piece of text. To do this, replace the text in the regex with the text you are interested in:
--------------------------------------------------------------------------------

PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX pav: <http://purl.org/pav/>

SELECT ?item ?source ?text ?annCreatedOn
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
WHERE {
  ?item a oa:SPLAnnotation;
    pav:createdOn ?annCreatedOn;
    pav:createdBy ?annCreatedBy;
    pav:createdWith ?createdWith.

  ?item oa:hasTarget ?target.
  ?target oa:hasSelector ?selector.
  ?selector a oa:PrefixSuffixTextSelector;
      oa:hasSource ?source;
      oa:exact ?text.FILTER(regex(?text,"The prevalence of this CYP2D6 phenotype varies widely and has been estimated at 0.5 to 1% in Chinese "))

}   

Results set in shorten URL: http://goo.gl/tKtZ22

--------------------------------------------------------------------------------
QUERY: Annotations are grouped into Collections, one per SPL resource; show the number of annotations in each annotation collection for CLOPIDOGREL product labels

Comment: the source uri won't matched in most cases 

same product name but there are different srouce uri in spl-core and pharmgx consensus data
ex1: http://dailymed.nlm.nih.gov/dailymed/lookup.cfm?setid=edae8df1-caf9-ff72-1304-5ae8b355f8e7
ex2: http://dailymed.nlm.nih.gov/dailymed/lookup.cfm?setid=03bec9ad-cd22-44d3-9b82-2796157de1d1

update linkedSPLs probably can fix this mismatching problem
--------------------------------------------------------------------------------

PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX cnt: <http://www.w3.org/2011/content#>
PREFIX linkedspls: <file:///home/rdb20/Downloads/d2rq-0.8.1/linkedSPLs-dump.nt#structuredProductLabelMetadata/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX pav: <http://purl.org/pav/>

SELECT ?item ?annCreatedOn ?annCreatedBy ?setId COUNT(?item) ?source WHERE {
  ?splUri a linkedspls_vocabulary:structuredProductLabelMetadata;
  linkedspls_vocabulary:setId ?setId;
  foaf:homepage ?source;
  linkedspls_vocabulary:activeMoiety ?activeMoiety.
  ?activeMoiety rdfs:label "CLOPIDOGREL".   
 
  ?item a oa:SPLAnnotation;
  pav:createdOn ?annCreatedOn;
  pav:createdBy ?annCreatedBy.

  ?item oa:hasTarget ?target.
  ?target oa:hasSelector ?selector.
  ?selector a oa:PrefixSuffixTextSelector;
      oa:hasSource ?source.

}  

--------------------------------------------------------------------------------
QUERY: Show for all annotations for a given CLOPIDOGREL/CYP2C19 occuring in the boxedWarning  section
--------------------------------------------------------------------------------

PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>

SELECT ?item ?source ?text 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
WHERE {
  ?item a oa:SPLAnnotation;
       oa:hasTarget ?target.

  ?target oa:hasSelector ?selector.
  ?selector oa:hasSource ?source;
      oa:exact ?text.

  ?item oa:hasBody ?body.
  ?body dailymed:pharmgxDrug "Clopidogrel";
        sio:SIO_000111 "34066-1 BOXED WARNING SECTION";
        dailymed:HGNCGeneSymbol "CYP2C19".
}  

Results set in shorten URL: http://goo.gl/cFpkqd

--------------------------------------------------------------------------------
QUERY: Show all annotations of any SPL boxedWarning section
--------------------------------------------------------------------------------

PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>

SELECT ?drug ?gene ?biomarker ?text 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
WHERE {
  ?item a oa:SPLAnnotation;
       oa:hasTarget ?target.

  ?target oa:hasSelector ?selector.
  ?selector oa:hasSource ?source;
      oa:exact ?text.

  ?item oa:hasBody ?body.
  ?body dailymed:pharmgxDrug ?drug;
        sio:SIO_000111 "34066-1 BOXED WARNING SECTION";
        dailymed:HGNCGeneSymbol ?gene;
        dailymed:pharmgxBiomarker ?biomarker.
}  
GROUP BY ?drug
ORDER BY ?drug 

Results set in shorten URL: http://goo.gl/7zSxSw

--------------------------------------------------------------------------------
QUERY: Show any pharmacodynamic impact for HLA-B*5701      
--------------------------------------------------------------------------------

PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>

SELECT ?drug ?section ?text 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
WHERE {
  ?item a oa:SPLAnnotation;
       oa:hasTarget ?target.

  ?target oa:hasSelector ?selector.
  ?selector oa:hasSource ?source;
      oa:exact ?text.

  ?item oa:hasBody ?body.
  ?body dailymed:pharmgxDrug ?drug;
        sio:SIO_000111 ?section;
        dailymed:pharmgxBiomarker "HLA-B*5701";
        poc:PharmacodynamicImpact ?pd.

}  
GROUP BY ?section
ORDER BY ?drug 

Results set in shorten URL: http://goo.gl/mwymrO

--------------------------------------------------------------------------------
QUERY: Show any biomarkers expected to have a pharmacokinetic impact on any drug
--------------------------------------------------------------------------------

PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX gcds: <http://www.genomic-cds.org/ont/genomic-cds.owl#>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>

SELECT ?pkImpact ?pdImpact ?gene ?drug ?section ?text 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
WHERE {
  ?item a oa:SPLAnnotation;
      oa:hasTarget ?target.

  ?target oa:hasSelector ?selector.
  ?selector a oa:PrefixSuffixTextSelector;
      oa:hasSource ?source;
      oa:exact ?text.

   ?item oa:hasBody ?body.

   ?body dailymed:pharmgxDrug ?drug;
         dailymed:HGNCGeneSymbol ?gene;
         sio:SIO_000111 ?section;
         poc:PharmacokineticImpact ?pkImpact;
         poc:PharmacodynamicImpact ?pdImpact.

}  
GROUP BY ?pkImpact
ORDER BY ?gene

Results set in shorten URL: http://goo.gl/58RIUJ


################################################################################
# INTEGRATION WITH linkedSPLs 
# Queries below are join linkedSPLs core graph and pharmacists's annotation graph  
################################################################################

--------------------------------------------------------------------------------
What do product labels say about for Pharmgkb PA124 (CYP2C19)?
--------------------------------------------------------------------------------

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX cnt: <http://www.w3.org/2011/content#>
PREFIX gcds: <http://www.genomic-cds.org/ont/genomic-cds.owl#>
PREFIX linkedspls: <file:///home/rdb20/Downloads/d2rq-0.8.1/linkedSPLs-dump.nt#structuredProductLabelMetadata/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX fn: <http://www.w3.org/2005/xpath-functions/#>

SELECT DISTINCT ?text ?drug ?section ?biomarker ?setId 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
FROM <http://purl.org/net/nlprepository/spl-core>
WHERE {
  ?pgx a linkedspls_vocabulary:pharmgxData;
      linkedspls_vocabulary:pharmgxXref <http://bio2rdf.org/pharmgkb:PA124>;
      linkedspls_vocabulary:pharmgxDrug  ?drug;
      linkedspls_vocabulary:pharmgxBiomarker ?biomarker;
      linkedspls_vocabulary:setId ?setId;
      linkedspls_vocabulary:pharmgxSPLSection ?section.

  ?spl a linkedspls_vocabulary:structuredProductLabelMetadata;
      linkedspls_vocabulary:setId ?setId.

  ?item a oa:SPLAnnotation;
       oa:hasTarget ?target.
  ?target oa:hasSelector ?selector.
  ?selector a oa:PrefixSuffixTextSelector;
      oa:hasSource ?source;
      oa:exact ?text.

   ?item oa:hasBody ?body.
   ?body dailymed:pharmgxDrug ?drug2.FILTER(?drug = fn:upper-case(?drug2))

   ?body dailymed:HGNCGeneSymbol ?biomarker.

}
GROUP BY ?drug
ORDER BY ?drug
LIMIT 10


--------------------------------------------------------------------------------
For which drugs do product labels associate  Pharmgkb PA124 (CYP2C19) with a toxicity?
--------------------------------------------------------------------------------

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX cnt: <http://www.w3.org/2011/content#>
PREFIX gcds: <http://www.genomic-cds.org/ont/genomic-cds.owl#>
PREFIX linkedspls: <file:///home/rdb20/Downloads/d2rq-0.8.1/linkedSPLs-dump.nt#structuredProductLabelMetadata/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX fn: <http://www.w3.org/2005/xpath-functions/#>

SELECT DISTINCT ?text ?drug ?section ?setId 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
FROM <http://purl.org/net/nlprepository/spl-core>
WHERE {
  ?pgx a linkedspls_vocabulary:pharmgxData;
      linkedspls_vocabulary:pharmgxXref <http://bio2rdf.org/pharmgkb:PA124>;
      linkedspls_vocabulary:pharmgxDrug  ?drug;
      linkedspls_vocabulary:pharmgxBiomarker ?biomarker;
      linkedspls_vocabulary:setId ?setId;
      linkedspls_vocabulary:pharmgxSPLSection ?section.

  ?spl a linkedspls_vocabulary:structuredProductLabelMetadata;
      linkedspls_vocabulary:setId ?setId.

  ?item a oa:SPLAnnotation;
       oa:hasTarget ?target.
  ?target oa:hasSelector ?selector.
  ?selector a oa:PrefixSuffixTextSelector;
      oa:hasSource ?source;
      oa:exact ?text.

   ?item oa:hasBody ?body.
   ?body dailymed:pharmgxDrug ?drug2.FILTER(?drug = fn:upper-case(?drug2))

   ?body dailymed:HGNCGeneSymbol ?biomarker;
         poc:PharmacodynamicImpact "Increased Toxicity Risk".

}
GROUP BY ?drug
ORDER BY ?drug
LIMIT 10

Results set in shorten URL: http://goo.gl/6q0i7I

--------------------------------------------------------------------------------
For which drugs do product labels associate a biomarker with toxicity?
--------------------------------------------------------------------------------

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX cnt: <http://www.w3.org/2011/content#>
PREFIX gcds: <http://www.genomic-cds.org/ont/genomic-cds.owl#>
PREFIX linkedspls: <file:///home/rdb20/Downloads/d2rq-0.8.1/linkedSPLs-dump.nt#structuredProductLabelMetadata/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX fn: <http://www.w3.org/2005/xpath-functions/#>

SELECT ?biomarker ?HGNC ?pharmgkb ?drug ?section ?text ?setId 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
FROM <http://purl.org/net/nlprepository/spl-core>
WHERE {
  ?pgx a linkedspls_vocabulary:pharmgxData;
      linkedspls_vocabulary:pharmgxXref ?pharmgkb;
      linkedspls_vocabulary:pharmgxDrug  ?drug;
      linkedspls_vocabulary:pharmgxBiomarker ?biomarker;
      linkedspls_vocabulary:HGNCGeneSymbol ?HGNC;
      linkedspls_vocabulary:setId ?setId;
      linkedspls_vocabulary:pharmgxSPLSection ?section.

  ?spl a linkedspls_vocabulary:structuredProductLabelMetadata;
      linkedspls_vocabulary:setId ?setId.

  ?item a oa:SPLAnnotation;
       oa:hasTarget ?target.
  ?target oa:hasSelector ?selector.
  ?selector a oa:PrefixSuffixTextSelector;
      oa:hasSource ?source;
      oa:exact ?text.

   ?item oa:hasBody ?body.
   ?body dailymed:pharmgxDrug ?drugLabel.FILTER(?drug = fn:upper-case(?drugLabel))

   ?body poc:PharmacodynamicImpact "Increased Toxicity Risk";
         dailymed:HGNCGeneSymbol ?biomarker.
}
GROUP BY ?biomarker
ORDER BY ?biomarker
LIMIT 10


--------------------------------------------------------------------------------
Show the annotations on product label statements for Pharmgkb PA124 (CYP2C19)
--------------------------------------------------------------------------------

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX cnt: <http://www.w3.org/2011/content#>
PREFIX gcds: <http://www.genomic-cds.org/ont/genomic-cds.owl#>
PREFIX linkedspls: <file:///home/rdb20/Downloads/d2rq-0.8.1/linkedSPLs-dump.nt#structuredProductLabelMetadata/>
PREFIX dailymed: <http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX linkedspls_vocabulary: <http://bio2rdf.org/linkedspls_vocabulary:>
PREFIX fn: <http://www.w3.org/2005/xpath-functions/#>

SELECT DISTINCT ?text ?drug ?section ?setId ?pdImpact ?pkImpact ?monitRec ?drugRec ?doseRec ?testRec 
FROM <http://purl.org/net/nlprepository/post-processed-pgx-consensus-oa-10072014>
FROM <http://purl.org/net/nlprepository/spl-core>
WHERE {
  ?pgx a linkedspls_vocabulary:pharmgxData;
      linkedspls_vocabulary:pharmgxXref <http://bio2rdf.org/pharmgkb:PA124>;
      linkedspls_vocabulary:pharmgxDrug  ?drug;
      linkedspls_vocabulary:pharmgxBiomarker ?biomarker;
      linkedspls_vocabulary:setId ?setId;
      linkedspls_vocabulary:pharmgxSPLSection ?section.

  ?spl a linkedspls_vocabulary:structuredProductLabelMetadata;
      linkedspls_vocabulary:setId ?setId.

  ?item a oa:SPLAnnotation;
        oa:hasTarget ?target.

  ?target oa:hasSelector ?selector.

  ?selector oa:hasSource ?source;
      oa:exact ?text.

  ?item oa:hasBody ?body.

  ?body dailymed:pharmgxDrug ?drugLabel.FILTER(?drug = fn:upper-case(?drugLabel))

      OPTIONAL {?body dailymed:HGNCGeneSymbol ?gene. }
      OPTIONAL {?body dailymed:pharmgxBiomarker ?biomarker. }
      OPTIONAL {?body poc:PharmacokineticImpact ?pkImpact. }
      OPTIONAL {?body poc:PharmacodynamicImpact ?pdImpact. }
      OPTIONAL {?body poc:MonitoringRecommendation ?monitRec. }
      OPTIONAL {?body poc:TestRecommendation ?testRec. }
      OPTIONAL {?body poc:DrugSelectionRecommendation ?drugRec. }
      OPTIONAL {?body poc:DoseSelectionRecommendation ?doseRec. }
}

LIMIT 10

Results set in shorten URL: http://goo.gl/7KcMBj

################################################################################
# GRAPH: http://purl.org/net/nlprepository/ddi-expert2-11-20
# This graph contains drug-drug interactions annotations from experts. 
################################################################################

PREFIX oa:<http://www.w3.org/ns/oa#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dikbD2R:<http://dbmi-icode-01.dbmi.pitt.edu:2020/vocab/resource/>
PREFIX domeo:<http://www.w3.org/ns/domeo#>
PREFIX pav:<http://purl.org/pav/>
PREFIX dailymed:<http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>

SELECT ?annot ?source ?exactText ?modality ?statementType ?drug1Lab ?drug1Type ?drug1Role ?drug2Lab ?drug2Type ?drug2Role
FROM <http://purl.org/net/nlprepository/ddi-expert2-11-20>

WHERE {

  ?annot a oa:expertstudyAnnotation;
     oa:body ?bn1;
     pav:versionNumber ?version;
     oa:context ?context.

  ?context oa:hasSelector ?selector;
           oa:hasSource ?source.
        
  ?selector a oa:PrefixSuffixTextSelector;
      oa:exact ?exactText.

  ?bn1 domeo:sets ?statement.

  ?statement a poc:DrugDrugInteractionStatement;
     dikbD2R:modality ?modality;
     dikbD2R:statement ?statementType;
     sio:SIO_000628 ?drugsInvolved.

  ?drugsInvolved a dikbD2R:PK_DDI;
     sio:SIO_000132 ?drug1;
     sio:SIO_000132 ?drug2.FILTER(?drug1 > ?drug2)
 
  ?drug1 rdfs:label ?drug1Lab.
  ?drug1 dailymed:activeMoietyRxCUI ?drug1rxc.
  ?drug1 rdf:type ?drug1Type.
  ?drug1 sio:SIO_000228 ?drug1Role.FILTER(?drug1Role!="xsd:String")

  ?drug2 rdfs:label ?drug2Lab.
  ?drug2 dailymed:activeMoietyRxCUI ?drug2rxc.
  ?drug2 rdf:type ?drug2Type.
  ?drug2 sio:SIO_000228 ?drug2Role.
}    
GROUP BY ?exactText
ORDER BY DESC(?annot)


Results set in shorten URL: http://goo.gl/ZQTX8n

--------------------------------------------------------------------------------
query ddi annotations, label 11 - 20, by dailymed URI with setid
--------------------------------------------------------------------------------

PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX poc: <http://purl.org/net/nlprepository/spl-pharmgx-annotation-poc#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dikbD2R:<http://dbmi-icode-01.dbmi.pitt.edu:2020/vocab/resource/>
PREFIX domeo:<http://www.w3.org/ns/domeo#>
PREFIX pav:<http://purl.org/pav/>
PREFIX dailymed:<http://dbmi-icode-01.dbmi.pitt.edu/linkedSPLs/vocab/resource/>

SELECT ?annot ?exactText ?modality ?statementType ?drug1Lab ?drug1Type ?drug1Role ?drug2Lab ?drug2Type ?drug2Role
FROM <http://purl.org/net/nlprepository/ddi-expert2-11-20>

WHERE {

  ?annot a oa:expertstudyAnnotation;
     oa:body ?bn1;
     pav:versionNumber ?version;
     oa:context ?context.

  ?context oa:hasSelector ?selector;
           oa:hasSource "http://dailymed.nlm.nih.gov/dailymed/drugInfo.cfm?setid=c25f968b-7037-4f60-9a02-2189769b0cbf".
        
  ?selector a oa:PrefixSuffixTextSelector;
      oa:exact ?exactText.

  ?bn1 domeo:sets ?statement.

  ?statement a poc:DrugDrugInteractionStatement;
     dikbD2R:modality ?modality;
     dikbD2R:statement ?statementType;
     sio:SIO_000628 ?drugsInvolved.

  ?drugsInvolved a dikbD2R:PK_DDI;
     sio:SIO_000132 ?drug1;
     sio:SIO_000132 ?drug2.FILTER(?drug1 > ?drug2)
 
  ?drug1 rdfs:label ?drug1Lab.
  ?drug1 dailymed:activeMoietyRxCUI ?drug1rxc.
  ?drug1 rdf:type ?drug1Type.
  ?drug1 sio:SIO_000228 ?drug1Role.FILTER(?drug1Role!="xsd:String")

  ?drug2 rdfs:label ?drug2Lab.
  ?drug2 dailymed:activeMoietyRxCUI ?drug2rxc.
  ?drug2 rdf:type ?drug2Type.
  ?drug2 sio:SIO_000228 ?drug2Role.
}    
GROUP BY ?exactText
ORDER BY DESC(?annot)


Results set in shorten URL: http://goo.gl/y1oiW5
